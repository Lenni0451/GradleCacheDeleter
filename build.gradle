plugins {
    id "java"
    id "checkstyle"
    id "net.raphimc.class-token-replacer" version "1.1.3"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    archivesName = project.maven_name ?: rootProject.maven_name
    group = project.maven_group ?: rootProject.maven_group
    version = project.maven_version ?: rootProject.maven_version
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    mavenCentral()
    maven {
        name = "lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
}

dependencies {
    compileOnly(annotationProcessor("org.projectlombok:lombok:1.18.34"))
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    include "net.lenni0451.commons:swing:1.6.0"
}

sourceSets {
    main {
        classTokenReplacer {
            //Replace all instances of '${version}' with the project version
            property("\${version}", project.version)
        }
    }
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    manifest {
        attributes(
                "Main-Class": "net.lenni0451.gradlecachedeleter.Main",
                "Multi-Release": "true"
        )
    }
}

checkstyle {
    toolVersion = "10.16.0" //Latest version for Java 8: 9.3
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
}

build.dependsOn(check)
