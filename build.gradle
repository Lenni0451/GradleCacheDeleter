plugins {
    id "java"
    id "checkstyle"
    id "io.freefair.lombok" version "8.4"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    archivesName = project.maven_name ?: rootProject.maven_name
    group = project.maven_group ?: rootProject.maven_group
    version = project.maven_version ?: rootProject.maven_version
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    mavenCentral()
    maven {
        name = "lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
}

dependencies {
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    include "net.lenni0451.commons:core:1.4.2"
    include "net.lenni0451.commons:swing:1.4.2"
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    manifest {
        attributes(
                "Main-Class": "net.lenni0451.gradlecachedeleter.Main",
                "Multi-Release": "true"
        )
    }
}

checkstyle {
    toolVersion = "9.3" //Latest version for Java 8
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
}

build.dependsOn(check)
